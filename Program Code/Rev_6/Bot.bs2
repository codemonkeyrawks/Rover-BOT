' {$STAMP BS2}
' {$PBASIC 2.5}
'*********************************************************************************************************
'* Program:  Bot.BS2                 Author:   Brian & Charlie                                           *
'* Date:     3/18/2010               Revision: 6.0                                                       *
'*********************************************************************************************************

' --------[ Program Description ]-------------------------------------------------------

' --------[ I/O Definitions ]-----------------------------------------------------------
LfIrOut     PIN 0                   'Left IR LED, Output
LfIrIn      PIN 1                   'Left IR Sensor, Input
RtIrOut     PIN 2                   'Right IR LED, Output
RtIrIn      PIN 3                   'Right IR Sensor, Input
TxEnable    PIN 4                   'Transmitter(27982)TR PIN
Tx          PIN 5                   'Transmitter(27982)DATA pin
WiFi        PIN 10                  'Wireless Data TX=1 RX=0
Speaker     PIN 11                  'Buzzer, Output
Servo_AB    PIN 12                  'Left + Right Servos
Servo_CD    PIN 14                  'Up/Down + Claw Servos
Ping        PIN 15                  'Ping sensor on pin 15
' --------[ Constants ]-----------------------------------------------------------------
Zone        CON     4000            'Buzzer Frequency
T2400       CON     396             'Baud Rate 2400
T9600       CON     84              'Baud Rate 9600
T19K2       CON     32              'Baud Rate 12000
Trigger     CON     5               'trigger pulse = 10 uS
Scale       CON     $200            'raw x 2.00 = uS
LcdBaud     CON     T19K2           'baud rate for LCD
LcdBkSpc    CON     $08             'move cursor left
LcdRt       CON     $09             'move cursor right
LcdLF       CON     $0A             'move cursor down 1 line
LcdCls      CON     $0C             'clear LCD (use PAUSE 5 after)
LcdCR       CON     $0D             'move pos 0 of next line
LcdOff      CON     $15             'LCD off
LcdLine1    CON     $80             'move to line 1, column 0
LcdLine2    CON     $94             'move to line 2, column 0
inscale     CON     872             'scale factor for inches at 2000 feet
' --------[ Variables ]-----------------------------------------------------------------
irDetectLeft     VAR  Bit           'Temporary Storage for IR Left
irDetectRight    VAR  Bit           'Temporary Storage for IR Right
distanceLeft     VAR  Nib           'Right Distance, Value
distanceRight    VAR  Nib           'Left Distance, Value
freqSelect       VAR  Nib           'Value of Frequency
Pos              VAR  Byte          'Position of Joystick Value
irFrequency      VAR  Word          'Temporary Storage for Frequency
DispNote         VAR  Word          'Count for FREQOUT
TxValue          VAR  Word          'Stores Count, Transmitting Data
RxValue          VAR  Word          'Stores Count, Receiving Data
Distance         VAR  Word          'Ping Distance Measurment
' --------[ Initialization for Servos and Ping ]----------------------------------------
DEBUG CLS, "Powering On, Please Wait..."  'Debug Welcome Screen
FREQOUT Speaker, 500, Zone                'Send a Test Note on Powerup
DEBUG CLS                                 'Clear Screen
' Servo Pal - Left + Right Servo:
INPUT Servo_AB                            'Make Sure Servos are Not Moving.
DO : LOOP UNTIL Servo_AB                  'Wait for ServoPAL to Power Up.
LOW Servo_AB                              'Set Pin, and Hold
PAUSE 100                                 '100mS Reset, ServoPAL.
HIGH Servo_AB                             'Raise PIN
PAUSE 100                                 'Wait for PIN, Active
' --------[Initialize Wireless TX ]-----------------------------------------------------
HIGH WiFi                                 'LED TX=1
HIGH TxEnable                             'Enable Transmitter
PULSOUT Tx,1200                           'Send Sync Pulse to Radio
SEROUT Tx, 24660, ["UUUU!"]               'Sends Data
TxValue = TxValue + 1                     'Increment Tx Value
PAUSE 10
'---------[Initialize Wireless RX ]-----------------------------------------------------
LOW WiFi                                  'LED RX=0
LOW TxEnable                              'Enable Receiver
SERIN Tx, 24660, [WAIT("UUUU!")]          'Receives Data
RxValue = RxValue + 1                     'Increment Rx Value
PAUSE 10
' --------[Set Values ]-----------------------------------------------------------------
RxValue = 0                               'Counter for Wireless RX
TxValue = 0                               'Counter for Wireless TX
'---------[Main Program ]---------------------------------------------------------------
MAIN:

DO

  GOSUB IR                                'IR Data Measurement
  GOSUB PingOut                           'Ping Data Measurement
  GOSUB Wireless                          'Receives Data IN

  IF Pos = 0 THEN GOSUB Neutral           'Robot: Neutral
  IF Pos = 1 THEN GOSUB Forward           'Robot: Forward
  IF Pos = 2 THEN GOSUB Backward          'Robot: Backward
  IF Pos = 3 THEN GOSUB Right             'Robot: Right
  IF Pos = 4 THEN GOSUB Left              'Robot: Left

LOOP

' --------[Subroutines ]----------------------------------------------------------------

IR:

    distanceLeft = 0
    distanceRight = 0

    FOR freqSelect = 0 TO 4

    LOOKUP freqSelect,[37500,38250,39500,40500,41500], irFrequency ' Table Lookup

    FREQOUT LfIrOut,1,irFrequency                                  ' IR Left, Transmit
    irDetectLeft = IN1                                             ' Store Distance

    FREQOUT RtIrOut,1,irFrequency                                  ' IR Right, Transmit
    irDetectRight = IN3                                            ' Store Distance

    distanceLeft = distanceLeft + irDetectLeft                     ' Sum Values, Left
    distanceRight = distanceRight + irDetectRight                  ' Sum Values, Right

  NEXT

  IF Pos = 0 THEN GOSUB Neutral           'Robot: Neutral
  IF Pos = 1 THEN GOSUB Forward           'Robot: Forward
  IF Pos = 2 THEN GOSUB Backward          'Robot: Backward
  IF Pos = 3 THEN GOSUB Right             'Robot: Right
  IF Pos = 4 THEN GOSUB Left              'Robot: Left

RETURN

PingOut:
  PULSOUT Ping, 5                         'Set PIN 15 for 10uS
  PULSIN Ping, 5, Distance                'Wait for Pin to Low
  Distance = Distance ** inscale          'Scale Inches Down
  PAUSE 100                               'Wait
  DEBUG HOME ,Distance                    'Display Distance
RETURN

Wireless:
  HIGH WiFi                               'LED TX=1
  LOW TxEnable                            'Enable Receiver
  SERIN Tx, 24660, [WAIT("UUUU!"), Pos]   'Receives Data
  RxValue = RxValue + 1                   'Increments Rx Value
RETURN

Neutral:
 PULSOUT Servo_AB, 2000                   'Turn Right Servo Off.
 PULSOUT Servo_AB, 2000                   'Turn Left Servo Off.
RETURN

Forward:
  DO
 PULSOUT Servo_AB, 1000                   'Right Servo: Forward
 PULSOUT Servo_AB, 500                    'Left Servo: Forward
 GOSUB Wireless                           'Check Data IN
  LOOP WHILE Pos = 1                      'Loop if Pos = 1
RETURN

Backward:
  DO
   IF distanceLeft < 1 OR distanceRight < 1 THEN GOSUB Buzz 'Disables Backup Control, Alarm
    PULSOUT Servo_AB, 500                 'Right Servo: Reverse
    PULSOUT Servo_AB, 1000                'Left Servo: Reverse
    GOSUB Wireless                        'Check Data IN
  LOOP WHILE Pos = 2                      'Loop if Pos = 2
RETURN

Right:
  DO
    PULSOUT Servo_AB, 500                 'Right Servo: Reverse
    PULSOUT Servo_AB, 500                 'Left Servo: Forward
     HIGH 7                               'Activate Right Blinker
     PAUSE 10                             'Wait
     LOW 7                                'Deactivate Right Blinker
     PAUSE 10                             'Wait
    GOSUB Wireless                        'Check Data IN
  LOOP WHILE Pos = 3                      'Loop if Pos = 3
RETURN

Left:
  DO
    PULSOUT Servo_AB, 1000                'Right Servo: Forward
    PULSOUT Servo_AB, 1000                'Left Servo: Reverse
     HIGH 9                               'Activate Left Blinker
     PAUSE 10                             'Wait
     LOW 9                                'Deactivate Left Blinker
     PAUSE 10                             'Wait
    GOSUB Wireless                        'Check Data IN
  LOOP WHILE Pos = 4                      'Loop if Pos = 4
RETURN

Buzz:
  FREQOUT Speaker, 500, Zone              'Send Alarm Signal
  PULSOUT Servo_AB, 2000                  'Turn Right Servo Off.
  PULSOUT Servo_AB, 2000                  'Turn Left Servo Off.
GOTO Main
